plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.3'
}

apply plugin: 'java'
apply plugin: 'distribution'
apply plugin: 'eclipse'
apply plugin: 'idea'

defaultTasks('clean', 'build')

sourceCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

dependencies {
    compile project(':l2j_scripting_engine')

    compile 'com.mchange:c3p0:0.9.5.2'
    compile 'com.jolbox:bonecp:0.8.0.RELEASE'
    compile 'com.sun.mail:javax.mail:1.5.6'
    compile 'com.zaxxer:HikariCP:3.3.1'
    compile 'mysql:mysql-connector-java:8.0.11'
    compile 'org.mariadb.jdbc:mariadb-java-client:2.3.0'
    compile 'org.bitlet:weupnp:0.1.4'
    compile group: 'com.google.guava', name: 'guava', version: '27.0.1-jre'
    compile 'org.python:jython:2.2.1'
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'org.slf4j:slf4j-jdk14:1.7.25'
    compile 'org.mindrot:jbcrypt:0.4'

    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.7'

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.6'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.6'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.9.6'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.9.6'

    compile fileTree(dir: 'dist/libs', include: '*.jar')
    compile group: 'org.jdbi', name: 'jdbi3-core', version: '3.4.0'
    compile group: 'org.jdbi', name: 'jdbi3-sqlobject', version: '3.4.0'
    testCompile("org.assertj:assertj-core:3.11.0")
    testCompile "org.mockito:mockito-core:2.+"
    testCompile('org.junit.jupiter:junit-jupiter-api:5.2.0')

    testImplementation('org.junit.jupiter:junit-jupiter-engine:5.2.0')
}


test {
    useJUnitPlatform()
}

def generalManifest = manifest {
    attributes('Built-By': System.getProperty('user.name'),
            'Built-Date': new Date().format("yyyy-MM-dd HH:mm:ss"),
            'Implementation-URL': 'http://www.l2jserver.com/',
            'Class-Path': configurations.runtime.collect { '../libs/' + it.name }.join(' '))
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import org.gradle.plugins.ide.internal.generator.PropertiesPersistableConfigurationObject


jar {
    archiveFileName = 'l2jserver.jar'
    exclude('**/dbinstaller/**')
    exclude('**/loginserver/**')
    exclude('**/accountmanager/**')
    exclude('**/gsregistering/**')
    manifest {
        from(generalManifest)
        attributes('Main-Class': 'com.l2jserver.gameserver.GameServer')
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task loginJar(type: ShadowJar, dependsOn: classes) {
    from(sourceSets.main.output)
    archiveFileName = 'l2jlogin.jar'
    exclude('**/dbinstaller/**')
    exclude('**/gameserver/**')
    manifest {
        from(generalManifest)
        attributes('Main-Class': 'com.l2jserver.loginserver.L2LoginServer')
    }
    configurations = [project.configurations.runtimeClasspath]
}

task configuratorJar(type: ShadowJar, dependsOn: classes) {
    from(sourceSets.main.output)
    archiveFileName = 'L2J_Configurator.jar'
    include('**/configurator/**')
    include('**/i18n/**')
    include('**/images/**')
    exclude('**/accountmanager/**')
    exclude('**/dbinstaller/**')
    exclude('**/gameserver/**')
    exclude('**/gsregistering/**')
    exclude('**/log/**')
    exclude('**/loginserver/**')
    exclude('**/status/**')
    exclude('**/util/**')
    exclude('**/Config/**')
    exclude('**/ConnectionFactory/**')
    exclude('**/Server/**')
    manifest {
        from(generalManifest)
        attributes('Main-Class': 'com.l2jserver.tools.configurator.ConfigUserInterface')
    }
    configurations = [project.configurations.runtimeClasspath]

}

task dbInstLsJar(type: Jar, dependsOn: classes) {
    from(sourceSets.main.output)
    archiveFileName = 'dbinst_ls.jar'
    include '**/dbinstaller/**'
    include '**/images/**'
    include '**/util/**'
    include '**/SQLFilter**'
    exclude '**/LauncherGS*'
    manifest {
        from(generalManifest)
        attributes('Main-Class': 'com.l2jserver.tools.dbinstaller.LauncherLS')
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task dbInstGsJar(type: Jar, dependsOn: classes) {
    from(sourceSets.main.output)
    archiveFileName = 'dbinst_gs.jar'
    include('**/dbinstaller/**')
    include('**/images/**')
    include('**/util/**')
    include('**/SQLFilter**')
    exclude('**/LauncherLS*')
    manifest {
        from(generalManifest)
        attributes 'Main-Class': 'com.l2jserver.tools.dbinstaller.LauncherGS'
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

artifacts {
    archives loginJar, configuratorJar, dbInstLsJar, dbInstGsJar
}

task zip(type: Zip, dependsOn: build) {
    from('dist') {
        exclude('libs')
    }
    into('libs') {
        from(configurations.runtime)
    }
    into('game') {
        from(jar)
        from(configuratorJar)
    }
    into('login') {
        from(loginJar)
    }
    into('tools') {
        from(dbInstLsJar)
        from(dbInstGsJar)
    }

    def filename = 'L2J_Server_' + new Date().format("yyyy-MM-dd") + '.zip'
    archiveFileName = filename
    println 'Build in build/distributions/' + filename
}

build.finalizedBy(zip)

eclipse {
    project {
        name = 'L2J_Server'
        comment 'L2J Server Project'
        natures 'org.eclipse.buildship.core.gradleprojectbuilde'
    }
    jdt {
        file {
            whenMerged { jdt ->
                def propertiesField = PropertiesPersistableConfigurationObject.getDeclaredField('properties')
                propertiesField.accessible = true
                Properties properties = (Properties) propertiesField.get(jdt)
                properties.setProperty 'org.eclipse.jdt.core.compiler.problem.incompleteEnumSwitch', 'ignore'
            }
        }
    }
}
